REM ******************************************************************************
REM *       SCOPE: Database Library                                              *
REM * DESCRIPTION: Export Database ID property data from MAP database library    *
REM *  PROPERTIES: item.cid                                                      *
REM *              {library path root//relative path/file name.itm}              *
REM *              item.databaseid                                               *
REM *----------------------------------------------------------------------------*
REM *  CREATED BY: D.Young - Southland Industries/Irvine                         *
REM *      DIRECT: (949) 440-5039                                                *
REM *        CELL: (949) 701-0724                                                *
REM *       EMAIL: dyoung@southlandind.com                                       *
REM *        DATE: 07-08-2009                                                    *
REM *  REVISED BY:                                                               *
REM *      DIRECT: (xxx) xxx-xxxx                                                *
REM *        CELL: (xxx) xxx-xxxx                                                *
REM *       EMAIL: <user>@southlandind.com                                       *
REM *        DATE: mm-dd-yyyy                                                    *
REM ******************************************************************************

dim rootpath = mappath_items + "/"
dim pathfrom = "items"

REM *** DONT CHANGE ANYTHING AFTER THIS ***

requires task

dim cr = ascii(10)

REM ##########################################
REM ### GETRELATIVEFROM
REM ##########################################
function getrelativefrom( path, relativefrom )

  dim rellen=len(relativefrom)
  dim rel = instr(1, path, "\" + relativefrom + "\")
  dim rv

 
  if not rel then rel = instr(1, path, "/" + relativefrom + "/")

 
  if rel then
    rv = substring( path, rel+rellen+2, 0 )
  else
    rv = path
  endif

  return rv
endfunction


REM ##########################################
REM ### UPDATEITEM
REM ##########################################
function UpdateItem ( path )

  rem scan for all items in the specified folder
  dim filescan as filelocator
  filescan.scan(path, "*.itm", true, false)
 
  dim folderscan as filelocator
  folderscan.scan(path, "*.*", false, true)
 
  rem debug path + cr + filescan.filecount + " files and " + folderscan.foldercount + " found"
 
  dim lp, lp2

  dim progressfiles=(folderscan.foldercount = 0)
 
  if progressfiles then task.beginprogress(filescan.filecount)
 
  for lp=1 to filescan.filecount
 
    rem load the item
    dim item as itemstruct
 
    dim loadfile=filescan.file[lp]
    dim sf=getrelativefrom( path, pathfrom )
    dim relpath = sf
    sf=substring(sf,1,-1)
    sf=GetFilename(sf)
    rem show parent folder name and itm filename
    task.message = sf + cr + GetFilename( loadfile )
    dim ItmName = GetFileName( loadfile )
 
    if item.load(loadfile) then
      outfile.writeline(item.cid + "," + relpath + itmname + "," + item.databaseid)
    endif

    if task.aborted then
      lp=filescan.filecount
    endif

 
  if progressfiles then task.progress = task.progress + 1

  next lp

  if progressfiles then
    task.endprogress()
  else
    task.beginprogress( folderscan.foldercount )
  endif
 
  for lp=1 to folderscan.foldercount
 
    dim nextpath = folderscan.folder[lp]
 
    task.message = "Scanning " + cr + nextpath
 
    UpdateItem( nextpath )

    if task.aborted then
      lp=folderscan.foldercount
    endif

    if not progressfiles then task.progress = task.progress + 1
 
  next lp

  if not progressfiles then task.endprogress()
 
endfunction
 

task.beginprogress( 1 )
task.progress = 0
object outfile = new file(mappath_items + "/" + "SI-WriteAllDatabaseIDs.TXT", foroutput+istext)
if outfile.isopen then
  outfile.writeline("CID,ITEM NAME,DATABASE ID")
  UpdateItem( rootpath )
  outfile.close()
endif
task.endprogress()